#!/usr/bin/env DJANGO_SETTINGS_MODULE=settings twill-sh
################################################################################
#########   SETUP                                                    ###########
################################################################################
runfile twill/strict-html.twi
# Creating entities
extend_with 'register.test.test_utilities'
run 'organization, project = get_organization_project()'
run 'employee = get_employee(organization=organization)'
run 'task1 = Task(name="Test login screen", project=project, description="Testing the login screen")'
run 'task1.save()'
run 'task2 = Task(name="Test index screen", project=project, description="Testing the index screen")'
run 'task2.save()'
run 'employee.tasks.add(task1, task2)'

runfile twill/login.twi

# Validate new page
code 200
url http://localhost:8000/netunong/register/$
# The employee name should be presented
find '${employee.name}'
# All tasks should be listed
find '<select name="task">'
# There should be an empty option: task should not be required for working
# period creation
find '<option />'
find '<option value="${str(task1.id)}">${task1.name}</option>'
find '<option value="${str(task2.id)}">${task2.name}</option>'
# The operation is "open", that is, open a new working period.
find '<input type="hidden" name="operation" value="open" />'

################################################################################
#########   OPENING A PERIOD                                         ###########
################################################################################
# Starts a period
fv 1 intention 'write a Twill test'
fv 1 task ${str(task1.id)}
submit

# Should have the working period at the database
run 'wp = employee.last_working_period'
run 'assert not wp.is_complete()'
run 'assert wp.intended_task == task1'

# Validate result
code 200
url http://localhost:8000/netunong/register/$
# The intention of the open period should be presented.
find 'write a Twill test'
# Trying to verify if the task really appears as a label and not only as an
# option.
find '[^>]${str(task1.name)}[^>]'
# Also, the tasks should be listed as well
find '<select name="task">'
find '<option />'
find '<option value="${str(task1.id)}">${task1.name}</option>'
find '<option value="${str(task2.id)}">${task2.name}</option>'
find '<input type="hidden" name="working_period" value="${str(wp.id)}" />'
find '<input type="hidden" name="operation" value="close" />'

################################################################################
#########   CLOSING THE PERIOD                                       ###########
################################################################################
fv 1 execution 'write another Twill test'
fv 1 task ${str(task2.id)}
submit

# Returns to open page
code 200
url http://localhost:8000/netunong/register/$
find '${employee.name}'
find '<select name="task">'
find '<option />'
find '<option value="${str(task1.id)}">${task1.name}</option>'
find '<option value="${str(task2.id)}">${task2.name}</option>'
find '<input type="hidden" name="operation" value="open" />'

# Working period should be closed
run 'wp = employee.last_working_period'
run 'assert wp.is_complete()'
run 'assert wp.executed_task == task2'

################################################################################
#########   OPENING PERIOD WHITHOUT INTENTION: FAIL                  ###########
################################################################################
# Tries to start a period without intention; should fail.
fv 1 intention ''
fv 1 task ${str(task1.id)}
submit
code 200
url http://localhost:8000/netunong/register/$
# Since the period could not be created, the operation is still "open"
notfind '<input type="hidden" name="operation" value="close" />'
find '<input type="hidden" name="operation" value="open" />'
# There is an error message
find '<ul class="errorlist">'


################################################################################
#########   CLOSING PERIOD WHITHOUT INTENTION: FAIL                  ###########
################################################################################
# Now, effectively starts a period
fv 1 intention 'write another Twill test'
fv 1 task ${str(task1.id)}
submit

code 200
url http://localhost:8000/netunong/register/$
find 'write another Twill test'
find '<input type="hidden" name="operation" value="close" />'
# Tries to close a period without execution; should fail.
fv 1 execution ''
fv 1 task ${str(task1.id)}
submit
code 200
url http://localhost:8000/netunong/register/$
# Since the period could not be completed, the operation is still "close"
notfind '<input type="hidden" name="operation" value="open" />'
find '<input type="hidden" name="operation" value="close" />'
# There is an error message
find '<ul class="errorlist">'

# Let us close it for now
fv 1 execution 'yes, yes, write another Twill test'
fv 1 task ${str(task2.id)}
submit
code 200
url http://localhost:8000/netunong/register/$
find '<input type="hidden" name="operation" value="open" />'

################################################################################
#########   OPENING PERIOD WHITHOUT TASK: ACCEPTABLE                 ###########
################################################################################
# Starts a period
fv 1 intention 'write a Twill test for no task'
fv 1 task ''
submit

# Should have the working period at the database
run 'wp = employee.last_working_period'
run 'assert not wp.is_complete()'
run 'assert wp.intended_task is None'

# Validate result
code 200
url http://localhost:8000/netunong/register/$
# The intention of the open period should be presented.
find 'write a Twill test for no task'
# No task can appear as a message, only as options in select
notfind '[^>]${str(task1.name)}[^>]'
notfind '[^>]${str(task2.name)}[^>]'
# Opening should be successful: operation now is "close"
find '<input type="hidden" name="operation" value="close" />'
# Also, the tasks should be listed as well
find '<select name="task">'
find '<option />'
find '<option value="${str(task1.id)}">${task1.name}</option>'
find '<option value="${str(task2.id)}">${task2.name}</option>'
find '<input type="hidden" name="working_period" value="${str(wp.id)}" />'

################################################################################
#########   CLOSING PERIOD WHITHOUT TASK: ACCEPTABLE                 ###########
################################################################################
fv 1 execution 'write another Twill test for no task'
fv 1 task ''
submit

# Returns to open page, without any problem
code 200
url http://localhost:8000/netunong/register/$
# Operation should be "open"
find '<input type="hidden" name="operation" value="open" />'

# Working period should be closed
run 'wp = employee.last_working_period'
run 'assert wp.is_complete()'
# No executed task
run 'assert wp.executed_task == None'


# Cleans the database
runfile twill/clean.twi

